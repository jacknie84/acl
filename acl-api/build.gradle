plugins {
  id 'idea'
  id 'java'
  id 'org.springframework.boot' version '2.6.5'
  id 'io.spring.dependency-management' version '1.0.11.RELEASE'
}

group = 'com.jacknie.examples'
version = '0.0.1'
sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

repositories {
  mavenCentral()
}

dependencies {
  annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
  annotationProcessor 'org.projectlombok:lombok'
  annotationProcessor 'jakarta.persistence:jakarta.persistence-api'
  annotationProcessor 'com.querydsl:querydsl-apt::jpa'

  compileOnly 'org.projectlombok:lombok'
  compileOnly 'com.google.code.findbugs:jsr305:3.0.2'

  developmentOnly 'org.springframework.boot:spring-boot-devtools'
  developmentOnly 'com.h2database:h2'

  implementation 'org.springframework.boot:spring-boot-starter-web'
  implementation 'org.springframework.boot:spring-boot-starter-validation'
  implementation 'org.springframework.boot:spring-boot-starter-integration'
  implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
  implementation 'org.springframework.boot:spring-boot-starter-data-redis'
  implementation 'org.springframework.boot:spring-boot-starter-security'
  implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
  implementation "org.springframework.security:spring-security-acl"
  implementation "org.springframework.integration:spring-integration-redis"
  implementation 'com.querydsl:querydsl-jpa'
  implementation 'org.springframework.data:spring-data-envers'
  implementation 'org.springframework.security:spring-security-oauth2-authorization-server:0.3.1'
  implementation('it.ozimov:embedded-redis:0.7.3') {
    exclude group: 'org.slf4j', module: 'slf4j-simple'
  }
}

task startTcpH2(type: JavaExec) {
  description = 'TCP H2 Server 실행'
  classpath = sourceSets.main.runtimeClasspath
  args += ['-tcp', '-tcpAllowOthers', '-baseDir', projectDir]
  mainClass.set('org.h2.tools.Server')
}

task startRedisServer(type: Exec) {
  description = 'Redis Server 실행'
  def basePath = "${projectDir}/redis-server/windows"
  commandLine 'cmd', '/c', "${basePath}/redis-server.exe", "${basePath}/redis.windows.conf"
}
